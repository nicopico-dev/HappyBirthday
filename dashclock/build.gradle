apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties props = new Properties()
def propertyFile = new File(project.projectDir, 'local.properties')
props.load(new FileInputStream(propertyFile))

android {
    compileSdkVersion sdk.compileSdk
    buildToolsVersion sdk.buildTools

    defaultConfig {
        applicationId 'fr.nicopico.dashclock.birthday'
        minSdkVersion sdk.minSdk
        targetSdkVersion sdk.targetSdk
        versionCode 14
        versionName '1.6.3'

        buildConfigField 'String', 'DEFAULT_LANG', '"en"'
    }

    signingConfigs {
        release {
            storeFile file(props.keystorePath)
            storePassword props.keystorePassword
            keyAlias props.keystoreKeyAlias
            keyPassword props.keystoreKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix 'DEBUG'
            resValue 'string', 'app_name_debug', 'DashClock Birthday DEBUG'
        }

        proguard {
            initWith debug
            applicationIdSuffix '.proguard'
            versionNameSuffix 'PROGUARD'
            resValue 'string', 'app_name_debug', 'DashClock Birthday PROGUARD'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    sourceSets {
        proguard {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
            res.srcDir 'src/debug/res'
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile project(':common')
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'

    compile dep.dagger
    kapt dep.dagger_compiler
    provided dep.jsr250
    provided dep.jsr305
    compile dep.kotlin
    compile dep.rxandroid
    compile dep.rxjava
    compile dep.timber
}