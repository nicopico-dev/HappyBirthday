apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties props = new Properties()
def propertyFile = new File(project.projectDir, 'local.properties')
props.load(new FileInputStream(propertyFile))

android {
    compileSdkVersion sdk.compileSdk
    buildToolsVersion sdk.buildTools

    defaultConfig {
        applicationId 'fr.nicopico.dashclock.birthday'
        minSdkVersion sdk.minSdk
        targetSdkVersion sdk.targetSdk
        versionCode 13
        versionName '1.6.2'

        buildConfigField 'String', 'DEFAULT_LANG', '"en"'
    }

    signingConfigs {
        release {
            storeFile file(props.keystorePath)
            storePassword props.keystorePassword
            keyAlias props.keystoreKeyAlias
            keyPassword props.keystoreKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        proguard {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warning 'MissingTranslation'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile project(':common')
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'

    compile dep.dagger
    kapt dep.dagger_compiler
    provided dep.jsr250
    provided dep.jsr305
    compile dep.kotlin
    compile dep.rxandroid
    compile dep.rxjava
    compile dep.timber
}